plugins {
    id 'java-library'
    id 'kotlin'
    id 'maven-publish'
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(file("github.properties")))
def githubUsername = githubProperties['library.user'] ?: System.getenv("GPR_USER")
def githubPublishKey = githubProperties['library.publish.key'] ?: System.getenv("GPR_API_KEY")

def getVersionName = { ->
    return "1.0.12"
}
//make sure the artifactId is lowercased
def getArtifactId = { ->
    return "serviceprovider"
}
def getGroupId = { ->
    return "com.outlivethesun"
}
def getGithubUrl = { ->
    return "https://maven.pkg.github.com/outlivethesun/ServiceProvider"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId getGroupId()
            artifactId getArtifactId()
            version getVersionName()
            artifact("$buildDir/libs/${getArtifactId()}.jar")
            artifact sourceJar

            //add all implementation dependencies to the pom file
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each { dep ->
                    def newDepNode = dependenciesNode.appendNode('dependency')
                    newDepNode.appendNode('groupId', dep.group)
                    newDepNode.appendNode('artifactId', dep.name)
                    newDepNode.appendNode('version', dep.version)
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri(getGithubUrl())
            credentials {
                username = githubUsername
                password = githubPublishKey
            }
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.reflections:reflections:0.10.2"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.5.30-RC"
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
}